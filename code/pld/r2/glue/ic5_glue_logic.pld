GAL22V10
GLUE

AS   IOSEL   HWRST  FC2    FC1     FC0   A1    A2    A3     A19      A18    GND
A17  A16     HALT   RESET  RUNLED  NC    NC    NC    BOOT   CPUSP    DUIACK VCC

HALT.T  =  GND
HALT.E  =  HWRST

RESET.T =  GND
RESET.E =  HWRST

RUNLED  = /HALT

AR      = /RESET * /HALT
BOOT.R  =  RESET *  FC2 * /FC1 *  FC0 * /A19 *  A18
        +  RESET *  FC2 * /FC1 *  FC0 *  A19 * /A18
        +  BOOT

/CPUSP  = /HWRST *  FC2 *  FC1 *  FC0 
/DUIACK = /HWRST *  FC2 *  FC1 *  FC0 * /AS * /A1 * /A2 * A3 * A19

DESCRIPTION

rosco_m68k glue for Classic board 2.0

Copyright (c)2022 Ross Bamford & Contributors
See LICENSE.md for licence details (spoiler: CERN / MIT).

This IC provides general reset and glue logic for rosco_m68k 
classic 2.0. It is broadly based on the reset glue from the pro.

