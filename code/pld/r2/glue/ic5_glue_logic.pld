GAL22V10
GLUE

AS   IOSEL   HWRST  FC2    FC1     FC0   A1    A2    A3     A19      A18    GND
A17  A16     HALT   RESET  RUNLED  BQ0   BQ1   BQ2   BOOT   CPUSP    DUIACK VCC

HALT.T  =  GND
HALT.E  =  HWRST

RESET.T =  GND
RESET.E =  HWRST

RUNLED  = /HALT

AR      = /HALT * /RESET
BQ0.R   = HALT + RESET              ; High after one memory cycle
BQ1.R   = HALT * BQ0 + RESET * BQ0  ; High after two memory cycles
BQ2.R   = HALT * BQ1 + RESET * BQ1  ; High after three memory cycles
BOOT.R  = HALT * BQ2 + RESET * BQ2  ; High after four memory cycles

/CPUSP  = /HWRST * FC0 * FC1 * FC2
/DUIACK = /HWRST * FC0 * FC1 * FC2 * /AS * /A1 * /A2 * A3 * A19

DESCRIPTION

rosco_m68k glue for Classic board 2.0

Copyright (c)2022 Ross Bamford & Contributors
See LICENSE.md for licence details (spoiler: CERN / MIT).

This IC provides general reset and glue logic for rosco_m68k 
classic 2.0. It is broadly based on the reset glue from the pro.

