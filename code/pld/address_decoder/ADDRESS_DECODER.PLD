Name     address_decoder ;
PartNo   00 ;
Date     14/07/2019 ;
Revision 03 ;
Designer Ross Bamford ;
Company  rosco_m68k ;
Assembly None ;
Location  ;
Device   g16v8a ;

/* *************** INPUT PINS *********************/
PIN 1   = A18;
PIN 2   = A19;
PIN 3   = A20;
PIN 4   = A21;
PIN 5   = A22;
PIN 6   = A23;
PIN 7   = UDS;
PIN 8   = LDS;
PIN 9   = BOOT;
PIN 11  = !oe;
PIN 18	 = IACK;

/* *************** OUTPUT PINS *********************/
PIN 12  = EVENRAMSEL;
PIN 13  = ODDRAMSEL;
PIN 14  = EVENROMSEL;
PIN 15  = ODDROMSEL;
PIN 16  = IOSEL;
PIN 17  = EXPSEL;

!EVENRAMSEL  = IACK & BOOT & !UDS & !A23 & !A22 & !A21 & !A20;
!ODDRAMSEL   = IACK & BOOT & !LDS & !A23 & !A22 & !A21 & !A20;

!EVENROMSEL = IACK & ((A18 & A19 & A20 & A21 & A22 & A23) # !BOOT) & !UDS;
!ODDROMSEL  = IACK & ((A18 & A19 & A20 & A21 & A22 & A23) # !BOOT) & !LDS;

!IOSEL      = IACK & !A18 & A20 & A21 & A22 & A23;		

!EXPSEL     = IACK & ((!A20 & A23) # (!A20 & A22) # (A21 & !A22) # (A21 & !A23) # (A20 & !A21) # (A18 & !A19 & A20));


EVENRAMSEL.OE = oe;
ODDRAMSEL.OE  = oe;
EVENROMSEL.OE = oe;
ODDROMSEL.OE  = oe;
IOSEL.OE      = oe;
EXPSEL.OE     = oe;
